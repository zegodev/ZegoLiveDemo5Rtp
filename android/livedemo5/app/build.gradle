apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.zego.livedemo5"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName getDemoVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi-v7a', 'x86', 'arm64-v8a' //, 'armeabi'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }

    testBuildType TEST_BUILD_TYPE

    signingConfigs {
        release {
            storeFile file("../../config/your_app_key.jks")
            storePassword "your_keystore_password"
            keyAlias "your_key_alias"
            keyPassword "your_key_password"
        }
    }
    
    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
            jniDebuggable true
        }
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            versionNameSuffix "-release"
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            pseudoLocalesEnabled true
            minifyEnabled false
        }
    }

    repositories {
        flatDir {
            dirs '3rdlibs'   // aar目录
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs', 'bizlibs', 'zegovideofilterlibs']
        }
    }
}



def getDemoVersionName(){
    String versionName = "1.0"
    File version_file = file("../demo_version.txt")
    if(version_file.exists()){
        versionName = version_file.readLines()[0]
    }
    return versionName
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation fileTree(include: ['*.jar'], dir: 'bizlibs')
    implementation fileTree(include: ['*.jar'], dir: '3rdlibs')
    implementation fileTree(include: ['*.jar'], dir: 'zegovideofilterlibs')
    implementation 'com.android.support:appcompat-v7:' + rootProject.supportLibVersion
    implementation 'com.android.support:design:' + rootProject.supportLibVersion
    implementation 'com.android.support:cardview-v7:' + rootProject.supportLibVersion
    implementation 'com.jakewharton:butterknife:7.0.1'
    implementation 'com.google.android.gms:play-services-appindexing:8.4.0'
    implementation 'com.google.code.gson:gson:2.7'
    implementation 'com.tencent.bugly:crashreport:2.2.2'
    implementation 'com.tencent.bugly:nativecrashreport:3.0'
    implementation 'eu.the4thfloor.volley:com.android.volley:2015.05.28'
    implementation 'com.google.guava:guava:18.0'
    implementation 'com.android.support:multidex:1.0.3'
    testImplementation 'junit:junit:4.12'
    // Testing-only dependencies
    // Force usage of support annotations in the test app, since it is internally used by the runner module.
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:' + rootProject.espressoVersion
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:' + rootProject.espressoVersion) {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    implementation(name: 'zego-qrcode', ext: 'aar')
    implementation(name: 'zego-app-support', ext: 'aar')
    implementation 'com.alibaba:fastjson:1.2.44'
    androidTestImplementation 'com.android.support.test:runner:' + rootProject.runnerVersion
    androidTestImplementation 'com.android.support.test:rules:' + rootProject.rulesVersion
    // Force usage of support annotations in the test app, since it is internally used by the
    // runner module.
    androidTestImplementation 'com.android.support:support-annotations:' + rootProject.supportLibVersion
    // Note that espresso-idling-resource is used in the code under test.
    implementation 'com.android.support.test.espresso:espresso-idling-resource:' + rootProject.espressoVersion
    implementation files('libs/zegoliveroom.jar')

}
